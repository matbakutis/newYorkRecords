buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-compose'

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}


task waitForUsersAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/users', 'Users API'
}

task waitForPostsAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/posts', 'Posts API'
}

task runUsersAPITests(type: Exec) {
	workingDir './users-api'
	commandLine './gradlew', 'test'
}

task runPostsAPITests(type: Exec) {
	workingDir './posts-api'
	commandLine './gradlew', 'test'
}

task allTests(type: Test) {
	dependsOn cleanTest
	dependsOn waitForUsersAPI
	dependsOn waitForPostsAPI
	dependsOn runUsersAPITests
	dependsOn runPostsAPITests

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

test {
	dependsOn cleanTest
	dependsOn waitForUsersAPI
	dependsOn waitForPostsAPI

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile 'com.google.guava:guava:24.1-jre'

	compileOnly 'org.projectlombok:lombok'

	runtime 'org.postgresql:postgresql'
	runtime 'org.springframework.boot:spring-boot-devtools'

	testCompile 'org.seleniumhq.selenium:selenium-java:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-api:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-remote-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-support:3.9.1'
	testCompile 'com.codeborne:selenide:4.10.01'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'io.rest-assured:rest-assured:3.0.6'
}

dockerCompose.isRequiredBy(test)
dockerCompose.isRequiredBy(allTests)